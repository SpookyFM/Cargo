// room script file

function hDoorDoorway_Look() {
  cEgo.Say("This door leads back to the entrance.");
}

function hDoorDoorway_Interact() {
  if (flagBridgerGone) {
    FadeOut(2);
      cSuperior.ChangeRoom(2);
      FadeIn(2);
      cSuperior.Say("There we are, seems like a malfunction.");
      cSuperior.Say("I disabled the security alert again, no worries.");
      flagSecurityAlert = false;
      flagBridgerGone = false;
      cEgo.Say("Good to know...");
  }
  cEgo.Loop = 1;
  cEgo.ChangeRoom(1, 865,  579);
  aDoorOpen.Play();
}


function hLock_UseInv()
{
  if (!flagBridgerGone) {
    Display("Before you do anything with the lock, you have to find a way to get rid of Bridger.");
    return;
  }
  
  if (cEgo.ActiveInventory == iResidueScanner) {
    if (cEgo.HasInventory(iKeycard)) {
      Display("You already have the keycard.");
    } else {
      StartCutscene(eSkipAnyKey);
      cEgo.Walk(753,  555, eBlock);
      Display("You point the residue scanner at the lock.");
      aResidueScanner.Play();
      Display("The sensor allows you to clearly see the fingerprints on the lock.");
      Display("Turns out that Bridger is not very creative with his combination...");
      aLockerOpen.Play();
      Display("You open the locker, and quickly look around in it.");
      cEgo.Say("There you are!");
      Display("You take the keycard and close the locker again before Brider can come back.");
      cEgo.AddInventory(iKeycard);
      aPickup.Play();
      FadeOut(2);
      cSuperior.ChangeRoom(2);
      FadeIn(2);
      cSuperior.Say("There we are, seems like a malfunction.");
      cSuperior.Say("I disabled the security alert again, no worries.");
      flagSecurityAlert = false;
      flagBridgerGone = false;
      
      cEgo.Say("Good to know...");
      EndCutscene();
    }
  } else {
    unhandled_event(1,  3);
  }
}

function hWindows_Look()
{
  Display("The large windows look out over the rest of the city in the distance.");
}

function hBench_Look()
{
  Display("Some benches are provided for the people working here to relax while they are not working.");
}

function hBench_Interact()
{
  Display("You don't want to sit down at the moment.");
}

function hFoodMachine_Look()
{
  Display("A food dispenser. If you were hungry, you could order from a rich menu of protein-enriched vitamin pastes.");
}

function hFoodMachine_Interact()
{
  Display("You are not hungry.");
}

function hFoodMachine_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("For a second you wonder what the machine might produce if it was subjected to the alien's energy fields, but you decide against it.");
  } else {
    unhandled_event(1,  3);
  }
}

function hLocker_Look()
{
  Display("These are lockers in which the workers of this facility can store their belongings.");
}

function hLocker_Interact()
{
  Display("These lockers are not interesting. However, the locker of Mr. Bridger might be.");
}

function hLockerBridger_Look()
{
  Display("The locker of your supervisor, Mr. Bridger.");
  if (!flagBridgerGone) {
    Display("As you look over the locker, you notice Bridger watching you carefully.");    
  }
}

function hLockerBridger_Interact()
{
  if (!flagBridgerGone) {
    Display("There's nothing you can do with the locker while Bridger is still here!");
  } else {
    Display("You try to open the locker, but it won't budge. Maybe you have to have a closer look at the keypad.");
  }
}

function hLockerBridger_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("You consider trying to manipulate the lock with the alien's energy fields, however, this might damage the lock beyond repair and you would not get to the keycard.");
  } else {
    unhandled_event(1,  3);
  }
}

function hLock_Look()
{
  Display("The lock of Bridger's locker. It's a flat surface that will light up and show a keypad once it is touched.");
}

function hLock_Interact()
{
  if (!flagBridgerGone) {
    Display("There's nothing you can do with the locker while Bridger is still here!");
  } else {
    Display("You consider trying to crack the combination, but it would take too long. Bridger might be back any second!");
  }
}
