// room script file

// Indicates that the player activated the force field emitters with the remote control
bool flagEmittersActive = false;

// Set to true when the fuse cover is open
bool flagFuseCoverOpen = false;

// Set to true when the charger is active
bool flagChargerActive = false;



// The room state
enum RoomState {
  State_Initial = 0, 
  State_AfterAccident = 1
};

RoomState roomState;

// Set the room state
// State_Initial -> On load (Cargo crates are enabled, sample box is disabled)
// State_AfterAccident -> After the incident with the alien
function set_room_status(RoomState state) {
  state = state;
  oCargoCratesBefore.Visible = (state == State_Initial);
  oSampleBox.Visible = !oCargoCratesBefore.Visible;
  oCargoCratesFallen.Visible = !oCargoCratesBefore.Visible;

  if (state == State_AfterAccident) {
    oEmitters.Graphic = 125;
    oChargingStation.Graphic = 169;
  } else {
    oEmitters.Graphic = 126;
    oChargingStation.Graphic = 168;
  }
}


function hDoorInterior_Interact()
{
  if (flagAlienFirstEncounter) {
    cEgo.Walk(860, 623, eBlock);
    Display("As you start moving towards the door, the emotions intensify! A wave of longing overcomes you, as if something was afraid that you were leaving it.");
    Display("These emotions must be coming from this... thing... creature?");
    Display("You decide to investigate this more before you get your supervisor.");
  } else {
    if (flagAlienTaken) {
      cEgo.Loop = 2;
      cEgo.ChangeRoom(1,  158,  603);
      
      aDoorOpen.Play();
    } else {
      // This is the case at the beginning of the game
      Display("You have to finish your task here first.");
    }
  }
}


function hDoorInterior_Look()
{
  cEgo.Say("This door leads to the interior of the landing platform.");
}





function room_FirstLoad()
{
  // Disable the hotspot(s) for the state after the force emitters failed
  set_room_status(0);
  
  SetBackgroundFrame(0);
  
  // Give Vala the PDA
  cEgo.AddInventory(iPDA);
  
  
  // Start the game
 
  StartCutscene(eSkipAnyKey);
  
  cEgo.Walk(670, 670,  eBlock);
  cEgo.Loop = 0;
  cEgo.Say("Let's see what I have to do today.");
  Display("You get out your PDA and check our assignments.");
  aPDA.Play();
  cEgo.Say("There's the cargo from the last expedition.");
  cEgo.Say("Pieces of rock from the far reaches of space.");
  cEgo.Say("*Sigh* Let's hope I get there as well when I finally manage to earn enough credits here to enroll in the academy.");
  cEgo.Say("But first I have to move these crates.");
  Display("Use the remote control to lift the crates with the force field emitted by the mechanism in the roof.");
  
  EndCutscene();
  
  
}

function hDoorRepair_Interact()
{
  if (flagAlienTaken) {
    cEgo.Loop = 1;
    cEgo.ChangeRoom(4,  700,  582);
    aDoorOpen.Play();
  } else 
    if (flagAlienFirstEncounter) {
      Display("That's the wrong way to go! Mr. Bridger is the other way!");
    } else {
      if (flagInvestigateAlien) {
        Display("No way you are going there right now! You have to find out what this thing is.");
      } else {
        Display("You decide to finish your task here first before checking out the work room.");
      }
    }
  }





function hSignRight_Look()
{
  cEgo.Say("The sign correctly says that this door leads to the interior of the landing platform.");
}

function oCargoCratesBefore_Look()
{
  Display("Standard issue containers for extraterrestrial samples. These are from the last expedition to the Alpha-7 sector. It's your job to move them from the cargo platform.");
}

function hCargoCrates_PickUp()
{
  cEgo.Say("I'm not lifting those things! There's the force field emitters for that.");
}

function hOpening_Look()
{
  Display("This opening connects the cargo room to the deck above. It is opened to allow cargo to be lowered and raised.");
}

function oRemoteControl_Interact()
{
  cEgo.Walk(743, 581,  eBlock);
  // TODO: Is there a better way?
  oRemoteControl.Visible = false;
  cEgo.AddInventory(iRemoteControl);
  aPickup.Play();
}

function oEmitters_Look()
{
  Display("The force field emitters can be turned on to lower objects from the opening in the ceiling and to transport them inside the room.");
}

function oEmitters_Interact()
{
  Display("You can start and stop the force field emitters with the remote control.");
}

function oCargoCratesBefore_Interact()
{
  cEgo.Say("I'm not lifting those things! There's the force field emitters for that.");
}

function oEmitters_UseInv()
{
  if (cEgo.ActiveInventory == iRemoteControl) {
    if (!flagEmittersActive) {
      cEgo.Walk(786, 652, eBlock);
      cEgo.Say("Here we go, time to move these things.");
      Display("You point the remote control at the force field emitters.");
      Display("A low, energetic sound tells you that the emitters have powered up and are ready to lift something.");
      oEmitters.Graphic = 124;
      aForceField.Play();
      flagEmittersActive = true;
    } else {
      Display("It's too early to power down the force emitters, finish moving the crates first.");
    }
  } 
}

function oCargoCratesBefore_UseInv()
{
  if (cEgo.ActiveInventory == iRemoteControl) {
    if (!flagEmittersActive) {
      Display("The force emitters are not powered up yet.");
    } else {
      Display("You point the remote control at the emitters and issue the command to lift the crates into the air.");
      cEgo.FaceLocation(800, 750);
      oBeams.Visible = true;
      aForceField.Play();
      Display("While the boxes are being lifted, something strange happens. This is not right!");
      ShakeScreen(5);
      aIntro.Play();
      cEgo.Say("Oh no! What's happening?!");
      oBeams.Visible = false;
      FadeOut(2);
      // SetBackgroundFrame(1);
      set_room_status(1);
      FadeIn(2);
      Display("You open your eyes again when the sound of the emitters has died down.");
      Display("Everything is in disarray, the boxes with the samples have fallen down.");
      cEgo.Say("This one is open. Uh oh, I'd better fix this or I loose my job...");
    }
  } 
}

function room_Load()
{
  // Disable the animating background
  if (roomState == State_Initial) {
    SetBackgroundFrame(0);
  } else {
    SetBackgroundFrame(1);
  }
  
    
  
}

function oSampleBox_Look()
{
  if (!flagAlienTaken) {
    cEgo.Say("This one has broken open. I'd better close it quickly.");
  } else {
    Display("You look at the box you found the alien in. It is completely destroyed.");
  }
}

function oSampleBox_Interact()
{
  if (!flagAlienTaken) {
    cEgo.Walk(713, 666, eBlock);
    Display("You bend down to close the box that has broken open.");
    cEgo.Say("Hey, there's something glowing in there.");
    Display("As you touch the box, a wave of emotions washes over you. In a torrent of emotions, you feel despair, fear and anger at the same time. With a start, you move away from the box.");
    cEgo.Walk(800, 666, eBlock);
    Display("Infront of your unbelieving eyes, a red apparition rises from the box.");
    aAlienAppear.Play();
    cAlien.ChangeRoom(3,  706, 720);
    cAlien.ManualScaling = true;
    cAlien.Scaling = 70;
    cAlien.Animate(1,  5, eOnce, eBlock, eForwards);
    cAlien.Animate(0, 3, eRepeat,  eNoBlock, eForwards);
    cEgo.Say("This is it, I'd better get help! Mr. Bridger should be somewhere, I'll get him!");
    flagAlienFirstEncounter = true;
    
  } else if (flagAlienFirstEncounter) {
    Display("Don't worry about the box - this apparition is much more important at the moment!");    
  } else {
    Display("You examine the box that the alien was in originally. There's no use for it anymore - it's busted beyond repair.");
  }
}

function oCargoCratesFallen_Look()
{
  Display("These crates have fallen down when the force field emitters failed.");
}



function room_LeaveLeft()
{
  if (flagAlienFirstEncounter & !flagInvestigateAlien) {
    cEgo.StopMoving();
    cEgo.Walk(800, 666, eBlock);
    cEgo.Say("I'm not going one step closer towards this thing!");
  }
}

function oRemoteControl_Look()
{
  Display("This is the remote control, used to control many of the systems found around here, such as the force field emitters at the ceiling.");
}



function hFuseCover_UseInv()
{
  
}

function hFuseCover_Interact()
{
  
}

function hCharger_UseInv()
{
  if (cEgo.ActiveInventory == iFuse) {
    if (flagFuseCoverOpen) {
      cEgo.Walk(788,  589,  eBlock);
      Display("You put the good fuse into the fuse box and close the cover. The charger turns on again.");
      aBox.Play();
      cEgo.LoseInventory(iFuse);
      oChargingStation.Graphic = 170;
      oFuse.Visible = true;
      flagChargerActive = true;
    } else {
      Display("You will have to get the fuse box open first.");
    }
  } else
  if (!flagChargerActive) {
    Display("Before you can put anything on the charger, you have to repair it. The incident with the alien seems to have blown the fuse.");
    return;
  }
  if (cEgo.ActiveInventory == iAlien) {
    if (flagAlienCharged) {
      Display("The alien is already nourished and the charge station is fried.");
    } else {
      Display("You put the alien onto the charging station.");
      Display("The charging station turns on with a small hum, that quickly grows to a pitch you haven't heard this machine go to before.");
      aCharge.Play();
      oChargingStation.Graphic = 169;
      Display("With a bang and a shower of sparks, the machine fails. Looks like it is gone for good now.");
      Display("However, as you touch the rock, it feels warm to your touch, and it sends you a positive feeling.");
      flagAlienCharged = true;
    }
  } else if (flagAlienCharged) {
    Display("This thing is not charging something again anytime soon.");
  } else if (cEgo.ActiveInventory == iPDA) {
    Display("The PDA is already charged.");
  } else if (cEgo.ActiveInventory == iRemoteControl) {
    Display("The remote control is already charged.");
  } else 
  if (cEgo.ActiveInventory == iBrokenFuse) {
    Display("There's no use in putting the broken fuse back in there.");
  } else {
    Display("This doesn't need to be charged.");
  }
  
    

  
}


function hDoorRepair_Look()
{
  Display("This door leads to a work room where small repairs can be carried out.");
}

function hCharger_Look()
{
  Display("This is the multi-purpose charge station. It is used to re-charge all types of appliances used around here.");
  if (!cEgo.HasInventory(iRemoteControl)) {
    Display("You placed your remote control for the tractor beams on there earlier.");
  }
}



function hWindows_Look()
{
  Display("Small windows in the wall allow you to see a glimpse of the city that stretches out beyond there.");
}

function hCargo_Look()
{
  Display("The walls are lined with cargo that is stored before it continues on it's way.");
}

function hCargo_Interact()
{
  cEgo.Say("I don't need to move this stuff. Besides, I wouldn't do it by hand - we have the force field emitters for that.");
}

function hDoorRepair_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("You consider holding the alien next to the door, but you fear that it's electrical fields might damage the door mechanism beyond repair.");
  } else {
    unhandled_event(1,  3);
  }
}

function hWindows_Interact()
{
  Display("These windows can't be opened.");
}

function hCharger_Interact()
{
  Display("There are no controls on the charging station. Just put anything you want charged on there.");
}

function oCargoCratesFallen_Interact()
{
  if (!flagAlienTaken) {
    cEgo.Say("I'd better deal with the one that has opened first.");
  } else {
    Display("You have a look at the fallen boxes. They look damaged, so you decide against taking one. Maybe there are new ones nearby?");
  }
}

function oCargoCratesFallen_UseInv()
{
  if (cEgo.ActiveInventory == iRemoteControl) {
    if (!flagAlienTaken) {
      cEgo.Say("The generators look like they are gone and besides, I have to figure out what caused this in the first place!");
    } else {
      Display("Don't worry about the boxes, the wellbeing of the alien is much more important now!");
    }
  } else if (cEgo.ActiveInventory == iAlien) {
    Display("A good idea, shielding the alien with one of the boxes.");
    Display("However, these look pretty damaged from the fall. Maybe there is a new one nearby?");
  } else
    {
    unhandled_event(1,  3);
  }
}

function oSampleBox_UseInv()
{
  if (cEgo.ActiveInventory == iRemoteControl) {
    if (!flagAlienTaken) {
      cEgo.Say("The generators look like they are gone and besides, I have to figure out what caused this in the first place!");
    } else {
      Display("Don't worry about the boxes, the wellbeing of the alien is much more important now!");
    }
  } else if (cEgo.ActiveInventory == iAlien) {
    Display("That's a good idea, trying to shield the alien inside such a box.");
    Display("However, this one is ruined. Maybe you can find a similar box?");
  } else {
    unhandled_event(1,  3);
  }
}









function oFuseCover_Interact()
{
  if (!flagFuseCoverOpen) {
    cEgo.Walk(788,  589,  eBlock);
    Display("You try to pry open the fuse cover, but it won't budge. You will need a tool for this.");
  } else {
    Display("The fuse cover is already open.");
  }
}


function oFuseCover_Look()
{
  Display("This is the cover of the fuse compartment for the charger.");
}

function oFuseCover_UseInv()
{
  if (cEgo.ActiveInventory == iScrewdriver) {
    cEgo.Walk(788,  589,  eBlock);
    if (!flagFuseCoverOpen) {
      Display("You open the fuse cover with your screwdriver.");
      aBox.Play();
      flagFuseCoverOpen = true;
      oFuseCover.Graphic = 152;
    } else {
      Display("The fuse cover is already open.");
    }
  }
  
}

function hFuseCompartment_Interact()
{
  Display("There is nothing else in there.");
}

function hFuseCompartment_Look()
{
  Display("This compartment houses the fuse of the charging station.");
}




function hFuseCompartment_UseInv()
{
}

function oBrokenFuse_Interact()
{
    cEgo.Walk(788,  589,  eBlock);
    cEgo.AddInventory(iBrokenFuse);
    aPickup.Play();
    Display("You take the broken fuse from the fuse box.");
    oBrokenFuse.Visible = false;
}

function oBrokenFuse_Look()
{
  Display("You see the reason why the charging station stopped working: The fuse has been blown.");
}

function oBrokenFuse_UseInv()
{
  if (cEgo.ActiveInventory == iFuse) {
    cEgo.Walk(788,  589,  eBlock);
    cEgo.AddInventory(iBrokenFuse);
    aPickup.Play();
    Display("You take the broken fuse from the fuse box and replace it with the new one.");
    oBrokenFuse.Visible = false;
    oFuse.Visible = true;
    cEgo.LoseInventory(iFuse);
    oChargingStation.Graphic = 170;
  }
}

function oFuse_Interact()
{
  Display("There's no need to remove the fuse now.");
}

function oFuse_Look()
{
  Display("You replaced the fuse in the charging station.");
}

function room_RepExec()
{
  if (flagAlienTaken) {
    if (oSampleBox.Graphic != 171) {
      oSampleBox.Graphic = 171; 
    }
  }
}
