// room script file


function hDoorCargoArea_Look()
{
  Display("This door leads to the cargo area.");
}

function hDoorCargoArea_Interact()
{
  cEgo.Loop = 1;
  cEgo.ChangeRoom(3,  840, 616);
  aDoorOpen.Play();
}

function hDoorCrewArea_Interact() 
{
  cEgo.Loop = 2;
  cEgo.ChangeRoom(2, 87,  610);
  aDoorOpen.Play();
}


function hKeycardScanner_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("You don't want to mess with the keycard mechanism. But maybe you could do something with the security scanner?");
  }
  else if (cEgo.ActiveInventory == iKeycard) {
    if (flagDoorOpen) {
      Display("The door is already open!");
    } else {
      cEgo.Walk(661, 551,  eBlock);
      cEgo.Say("Here goes nothing...");
      Display("You swipe the keycard across the reader.");
      oDisplay.Tint(0,  255,  0, 100,  100);
      aKeycard.Play();
      cComputer.Say("Card accepted. Please step through the door.");
      Display("The scanner will not give an alarm any more. You can step outside now!");
      aDoorOpen.Play();
      flagDoorOpen = true;
    }
  } else unhandled_event(1,  3);
}

function hDoorOutside_Interact()
{
  StartCutscene(eSkipAnyKey);
  if (!flagDoorOpen) {
    Display("The door will not open for you unless you have a key card that authorizes you.");
  } else {
    Display("You step towards the door anxiously.");
    cEgo.Walk(453,  512,  eBlock);
    if (!cEgo.HasInventory(iBoxWithAlien)) {
      aWarning.Play();
      oDisplay.Tint(255,  0,  0,  100,  100);
      cComputer.Say("Invalid energy signature detected. Possible breach of containment. Please check your equipment for contamination.");
      Display("You quickly step away from the security scanner.");
      cEgo.Walk(357, 615,  eBlock);
      Display("It has picked up the energy signature of the alien.");
      cEgo.Say("I'll have to find some way of shielding you from this.");
      EndCutscene();
    } else {
      Display("Nothing happens! The scanner has not detected the life form.");
      cEgo.Walk(456, 473,  eBlock);
      
      // Hide the character without a scene change
      int oldX = cEgo.x;
      cEgo.x = -100;
      Display("Quickly, you run outside and up to the ship.");
      Display("You see the ship infront of you and the open cargo area. Among the array of satellite deployment boxes, you find a free space.");
      Display("You put the alien inside and rush back, in case someone might see you.");
      cEgo.x = oldX;
      cEgo.Walk(468, 605,  eBlock);
      cEgo.Say("I guess that's it then... Now I have to clean up the mess that's left behind.");
      EndCutscene();
      cEgo.ChangeRoom(6);
    }
  }
  
}

function hDoorCargoArea_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("You consider holding the alien next to the door, but you already know that it might disrupt the electronical circuits and might shut the door.");
  } else {
    unhandled_event(1,  3);
  }
}

function hWindow_Look()
{
  Display("The city stretches out from here to the horizon. Since the landing platform is pretty high up, you can see far.");
}

function hTerminal_Look()
{
  Display("It's an information terminal intended for visitors.");
}

function hSecurityScanner_Look()
{
  Display("This is a security scanner. One of it's purposes is to scan for potential alien life forms to enforce the policy of no contact.");
  if (flagSecurityAlert) {
    Display("A security alert is sounding loudly. Only your supervisor, Mr. Bridger, can fix this.");
  } else {
    Display("You will have to find a way to get the alien through there.");
  }
}

function hDoorOutside_Look()
{
  Display("This door leads to an elevator that carries staff and crew members up to the landing platform.");
  if (flagDoorOpen) {
    Display("You managed to open the door!");
  }
}

function hKeycardScanner_Look()
{
  Display("It's the keycard scanner that regulates the access to the landing platform. At the moment, only those cleared for access to the ship have a keycard.");
}

function hDoorCrewArea_Look()
{
  Display("This door leads to the crew area.");
}

function hTerminal_Interact()
{
  Display("The terminal doesn't interest you.");
}

function hSecurityScanner_Interact()
{
  Display("Fiddling around with the sturdy-looking scanner will do nothing. Maybe there is another way to disrupt it?");
}

function hSecurityScanner_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    if (cEgo.HasInventory(iKeycard)) {
      Display("You already have the keycard, you'd better not draw any more attention to what you are doing.");
      return;
    }
    if (!flagSecurityAlert) {
      cEgo.Walk(297,  545, eBlock);
      Display("You take the alien out and hold it next to the scanner.");
      Display("I starts to wreak it's havoc on the poor machine. Within seconds, sparks are flying.");
      Display("Claxons start sounding, and you quickly take the alien away and hide it again.");
      aAlert.Play();
      flagSecurityAlert = true;
    } else {
      Display("You have already triggered a security alert.");
    }
  } else {
    unhandled_event(1,  3);
  }
}

function hKeycardScanner_Interact()
{
  Display("There are no controls on the keycard scanner except the mechanism to swipe the keycard.");
}

function hDoorCrewArea_UseInv()
{
  if (cEgo.ActiveInventory == iAlien) {
    Display("You know that the alien can disrupt electronic circuits, and you do not want to block this door, so you decide against using the alien on the door.");
  } else {
    unhandled_event(1,  3);
  }
}


function room_AfterFadeIn()
{
  if (flagSecurityAlert) {
    aAlert.Play();
  } else {
    aAlert.Stop();
  }
}

function room_Leave()
{
  aAlert.Stop();
}

int counter = 0;

function room_RepExec()
{
  if (flagSecurityAlert) {
    
     int phase = counter % 50;
     if (phase < 25) {
       oDisplay.Tint(0,  0,  0,  100,  100);
     } else {
        oDisplay.Tint(255,  0,  0, 100,  100);
     }
     counter += 1;
  }
}
